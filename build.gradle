buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
	}
}

plugins {
	id "com.github.johnrengelman.shadow" version "1.2.3"
}

apply plugin: "net.minecraftforge.gradle.forge"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mc_version + "-" + mod_version

archivesBaseName = "DiscordChat"

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings

	replaceIn "OneTwelveTwoMod.java"
	replace "@VERSION@", mod_version
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

repositories {
	jcenter()
	maven {
		name "shadowfacts"
		url "http://maven.shadowfacts.net/"
	}
	flatDir {
		dirs "libs"
	}
}

dependencies {
	compile group: "JDA", name: "JDA", version: jda_version, classifier: "withDependencies"
	compile group: "com.vdurmont", name: "emoji-java", version: emoji_version
	compile group: "org.apache.logging.log4j", name: "log4j-api", version: log4j_version
	compile group: "com.typesafe", name: "config", version: config_version
	compile group: "com.google.code.gson", name: "gson", version: gson_version
	compile group: "net.shadowfacts", name: "ShadowLib", version: shadowlib_version
}

shadowJar {
	classifier = ""
	relocate "com.iwebpp.crypto", "net.shadowfacts.discordchat.repack.com.iwebpp.crypto"
	relocate "com.neovisionaries.ws.client", "net.shadowfacts.discordchat.repack.com.neovisionaries.ws.client"
	relocate "com.sun.jna", "net.shadowfacts.discordchat.repack.com.sun.jna"
	relocate "com.vdurmont.emoji", "net.shadowfacts.discordchat.repack.com.vdurmont.emoji"
	relocate "gnu.trove", "net.shadowfacts.discordchat.gnu.trove"
	relocate "net.dv8tion.jda", "net.shadowfacts.discordchat.repack.net.dv8tion.jda"
	relocate "okhttp3", "net.shadowfacts.discordchat.repack.okhttp3"
	relocate "okio", "net.shadowfacts.discordchat.repack.okio"
	relocate "org.apache.commons.collections4", "net.shadowfacts.discordchat.repack.org.apache.commons.collections4"
	relocate "org.apache.commons.lang3", "net.shadowfacts.discordchat.repack.org.apache.commons.lang3"
	relocate "org.json", "net.shadowfacts.discordchat.repack.org.json"
	relocate "tomp2p.opuswrapper", "net.shadowfacts.discordchat.repack.tomp2p.opuswrapper"
}

reobf { shadowJar { mappingType = "SEARGE" } }
tasks.reobfShadowJar.mustRunAfter shadowJar
